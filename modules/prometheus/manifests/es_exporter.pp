# Prometheus Elasticsearch query metrics exporter.

class prometheus::es_exporter {
    package { 'prometheus-elasticsearch-exporter':
        ensure => present,
    }

    file { '/etc/prometheus-es-exporter':
        ensure  => directory,
        recurse => true,
        purge   => true,
        force   => true,
        owner   => 'root',
        group   => 'root',
        mode    => '0444',
        source  => 'puppet:///modules/prometheus/es_exporter',
        require => Package['prometheus-elasticsearch-exporter'],
        notify  => Service['prometheus-elasticsearch-exporter'],
    }

    # by default, prometheus-elasticsearch-exporter exports cluster, index, and node metrics generated by prometheus-elasticsearch-exporter
    # this unit override disables these metrics in prometheus-es-exporter
    systemd::service { 'prometheus-elasticsearch-exporter':
        ensure   => present,
        content  => init_template('prometheus-elasticsearch-exporter', 'systemd_override'),
        override => true,
        restart  => true,
    }

    $firewall_rules = query_facts('Class[Prometheus]', ['ipaddress', 'ipaddress6'])
    $firewall_rules_mapped = $firewall_rules.map |$key, $value| { "${value['ipaddress']} ${value['ipaddress6']}" }
    $firewall_rules_str = join($firewall_rules_mapped, ' ')
    ferm::service { 'prometheus es_exporter':
        proto  => 'tcp',
        port   => '9206',
        srange => "(${firewall_rules_str})",
    }
}
